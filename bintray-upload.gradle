apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/SimpleMobileTools/Simple-Commons'
def gitUrl = 'https://github.com/SimpleMobileTools/Simple-Commons.git'
group = "com.simplemobiletools"
version = propVersionName

install {
    repositories.mavenInstaller.pom.project {
        packaging 'aar'
        name 'Simple Commons'
        description = 'Some helper functions, dialogs, etc. used by my "simple" apps.'
        url siteUrl

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id 'tibbi'
                name 'Tibor Kaputa'
                email 'hello@simplemobiletools.com'
            }
        }
        scm {
            connection gitUrl
            developerConnection gitUrl
            url siteUrl
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

Properties properties = new Properties()
properties.load project.rootProject.file('local.properties').newDataInputStream()

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "commons"
        name = "commons"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}
